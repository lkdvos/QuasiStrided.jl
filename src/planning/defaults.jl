# The engine's per-host defaults for an element type, resolved once per
# (detected profile, eltype) and cached: every lookup in kernel_selection.jl and
# blocking.jl is keyed on `Val(profile.isa)`, a dynamic dispatch far too slow to
# repeat on every `plan_contract`.

# The default `shape` and whether it is an `fmaddsub` one, the `fitted` small-M demotion shape, the
# complex small-M FMAddSub candidates, the unscaled real blocking row and the
# core's L2 share, all derived from `profile`, which is the cache key.
struct ResolvedDefaults
    profile::TargetProfile
    shape::NTuple{3, Int}
    fmaddsub::Bool
    fitted::NTuple{3, Int}
    small_m::Vector{NTuple{3, Int}}
    real_row::Blocking
    l2_core::Int
end

function _resolve_defaults(profile::TargetProfile, ::Type{T}) where {T}
    method = _isa_method(Val(profile.isa), T)
    # First, so an element type with no menu throws from here.
    shape = _derived_shape(profile, T, method)
    fitted = _fitted_shape(profile, T, _default_method(T))
    small_m = _small_m_candidates(Val(profile.isa), profile, T)
    real_row = _real_blocking_row(profile, real(T))
    l2_core = _l2_core_bytes(profile)
    return ResolvedDefaults(profile, shape, method isa FMAddSubMethod, fitted, small_m, real_row, l2_core)
end

# One slot per supported element type (a method dispatch, not a `Dict` probe);
# other types take the uncached path and fail there.
const _DEFAULTS_F64 = Ref{Union{Nothing, ResolvedDefaults}}(nothing)
const _DEFAULTS_F32 = Ref{Union{Nothing, ResolvedDefaults}}(nothing)
const _DEFAULTS_C64 = Ref{Union{Nothing, ResolvedDefaults}}(nothing)
const _DEFAULTS_C32 = Ref{Union{Nothing, ResolvedDefaults}}(nothing)
_defaults_slot(::Type{Float64}) = _DEFAULTS_F64
_defaults_slot(::Type{Float32}) = _DEFAULTS_F32
_defaults_slot(::Type{ComplexF64}) = _DEFAULTS_C64
_defaults_slot(::Type{ComplexF32}) = _DEFAULTS_C32
_defaults_slot(::Type) = nothing

# Refilled whenever the slot's profile is not `===` the current one, so a
# profile installed after load (as test/forced_isa_runner.jl does) takes effect
# without an invalidation hook. Unlocked: a racing refill stores the same value.
@inline function _resolved_defaults(::Type{T}) where {T}
    slot = _defaults_slot(T)
    profile = target_profile()
    slot === nothing && return _resolve_defaults(profile, T)
    cached = slot[]
    (cached !== nothing && cached.profile === profile) && return cached
    return _refill_defaults!(slot, profile, T)
end

@noinline function _refill_defaults!(slot, profile::TargetProfile, ::Type{T}) where {T}
    fresh = _resolve_defaults(profile, T)
    slot[] = fresh
    return fresh
end

function _default_kernel(::Type{T}) where {T}
    d = _resolved_defaults(T)
    return _kernel_from_shape(d.shape, T, d.fmaddsub ? FMAddSubMethod() : _default_method(T))
end

# The automatic `(shape, method)` for extents `Qm`/`Qn` and C's unit-stride run
# along M (`run = Qm`: no layout known). Returned as plain values so
# `plan_contract` never holds a menu-wide kernel Union. The extent and store
# step-downs apply first; then an `Qm` that cannot fill one tile demotes to the
# fitted shape, or for complex on AVX-512/AVX2 to FMAddSub.
@inline function _default_shape(::Type{T}, Qm::Int, Qn::Int, run::Int = Qm) where {T}
    d = _resolved_defaults(T)
    # Static, so a real `T`'s method stays a concrete `RealMethod`.
    method = T <: Complex && d.fmaddsub ? FMAddSubMethod() : _default_method(T)
    shape = _store_shape(_extent_shape(d.shape, T, method, Qm), T, method, Qm, run)
    # Where C's rows defeat the vector store, planar's scalar store is the faster one.
    T <: Complex && d.fmaddsub && run != Qm && run % shape[1] != 0 && return (d.fitted, _default_method(T))
    (Qm > 0 && Qm < shape[1]) || return (shape, method)
    T <: Complex || return (d.fitted, method)
    small = _small_m_shape(d.small_m, Qm)
    small === nothing || return (small, FMAddSubMethod())
    return (d.fitted, _default_method(T))
end

# The automatic `(shape, method)` under `method`, `_default_method(T, TA, TB)`.
@inline _default_shape(::Type{T}, ::ComplexMethod, Qm::Int, Qn::Int, run::Int) where {T} =
    _default_shape(T, Qm, Qn, run)

# The real default shape of the real problem, mapped: the real extent, store and
# small-M demotions carry over, on the real type's cached defaults.
@inline function _default_shape(::Type{T}, method::_MixedMethod, Qm::Int, Qn::Int, run::Int) where {T}
    shape, _ = _default_shape(real(T), _real_problem(method, Qm, Qn, run)...)
    return (_mixed_shape(method, shape), method)
end

# `@noinline`: the return type is the Union of `T`'s menu kernels.
@noinline function _default_kernel(::Type{T}, Qm::Int, Qn::Int) where {T}
    shape, method = _default_shape(T, Qm, Qn)
    return _kernel_from_shape(shape, T, method)
end

"""
    default_blocking(kernel) -> Blocking

Cache-blocking factors for `kernel` on this host: an analytical model of the
detected cache geometry ([`target_profile`](@ref)), or fixed constants where
L1d or L2 is undetected, scaled for complex methods by packed reals per
element. `plan_contract` rounds `mc`/`nc` to `mr`/`nr` multiples and clamps
all three to the contraction's extents.
"""
function default_blocking(kernel)
    T = scalartype(kernel)
    return _scale_blocking(_resolved_defaults(T).real_row, complex_method(kernel))
end
